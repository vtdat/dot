pkgname=st
pkgver=0.8.1
pkgrel=1
pkgdesc="A simple virtual terminal emulator for X."
arch=("i686" "x86_64")
license=("MIT")
depends=("libxft" "libxext" "xorg-fonts-misc")
makedepends=("ncurses")
url="http://st.suckless.org"
provides=("st")
conflicts=("st")

_patches=("https://st.suckless.org/patches/clipboard/st-clipboard-0.8.1.diff"
          "https://st.suckless.org/patches/scrollback/st-scrollback-0.8.diff"
          "https://st.suckless.org/patches/scrollback/st-scrollback-mouse-0.8.diff"
          "https://st.suckless.org/patches/scrollback/st-scrollback-mouse-altscreen-0.8.diff")
source=("http://dl.suckless.org/st/$pkgname-$pkgver.tar.gz"
        ${_patches[@]})
sha1sums=("3ce42998720b9285186608bb38d88bb1c80a620a"
          "26e946870fa7ab3907cd6b8972ebbbd6a3aa0fe5"
          "623f474b62fb08e0b470673bf8ea947747e1af8b"
          "46e92d9d3f6fd1e4f08ed99bda16b232a1687407"
          "d3329413998c5f3feaa7764db36269bf7b3d1334")

prepare() {
  cd $srcdir/$pkgname-$pkgver

  # skip terminfo which conflicts with ncurses
  sed -i "/\@tic /d" Makefile

  for patch in ${_patches[@]}; do
      echo "Applying patch ${patch##*/}..."
      patch -Np1 < "${srcdir}/${patch##*/}"
  done

  echo "Copying config.def.h to $startdir..."
  cp config.def.h "${startdir}/"

  echo "Copying config.h from $startdir if it exists..."
  [ -f "${startdir}/config.h" ] && cp "${startdir}/config.h" . || true
}

build() {
  cd $srcdir/$pkgname-$pkgver

  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd $srcdir/$pkgname-$pkgver

  make PREFIX=/usr DESTDIR="$pkgdir" TERMINFO="$pkgdir/usr/share/terminfo" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 README "$pkgdir/usr/share/doc/$pkgname/README"
}
